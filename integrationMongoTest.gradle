sourceSets {
    integrationMongoTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationMongoTest/java')
        }
    }
}

configurations {
    integrationMongoTestImplementation.extendsFrom implementation
}

dependencies {

    // Spring
    integrationMongoTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationMongoTestImplementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    integrationMongoTestImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock' // TODO Remove!
    integrationMongoTestImplementation 'org.springframework.boot:spring-boot-starter-amqp'
    integrationMongoTestImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    integrationMongoTestImplementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    // Lombok
    integrationMongoTestCompileOnly 'org.projectlombok:lombok:1.18.26'
    integrationMongoTestAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    // Other
    integrationMongoTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.5.4'
    integrationMongoTestImplementation 'com.github.fridujo:rabbitmq-mock:1.1.1'
    integrationMongoTestImplementation 'org.awaitility:awaitility:4.1.0'
    integrationMongoTestImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'
    integrationMongoTestImplementation 'org.junit.platform:junit-platform-launcher:1.9.2'
    integrationMongoTestImplementation 'org.jeasy:easy-random-core:5.0.0'

    // LSD
    integrationMongoTestImplementation 'io.github.lsd-consulting:lsd-junit5:4.0.0'
    integrationMongoTestImplementation 'io.github.lsd-consulting:lsd-distributed-generator:4.0.1'
}

task integrationMongoTest(type: Test) {
    useJUnitPlatform()
    dependsOn assemble

    testClassesDirs = sourceSets.integrationMongoTest.output.classesDirs
    classpath = sourceSets.integrationMongoTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = false
    mustRunAfter(test)
    finalizedBy jacocoTestReport
}

check.dependsOn(integrationMongoTest)

integrationMongoTest {
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Mongo integration test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
