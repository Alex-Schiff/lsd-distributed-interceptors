sourceSets {
    integrationHttpTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationHttpTest/java')
        }
    }
}

configurations {
    integrationHttpTestImplementation.extendsFrom implementation
}

dependencies {

    // Spring
    integrationHttpTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationHttpTestImplementation 'org.springframework.boot:spring-boot-starter-amqp'
    integrationHttpTestImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    integrationHttpTestImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    integrationHttpTestImplementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    // Lombok
    integrationHttpTestCompileOnly 'org.projectlombok:lombok:1.18.26'
    integrationHttpTestAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    // Other
    integrationHttpTestImplementation 'com.github.fridujo:rabbitmq-mock:1.1.1'
    integrationHttpTestImplementation 'org.awaitility:awaitility:4.1.0'
    integrationHttpTestImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'

    // LSD
    integrationHttpTestImplementation 'io.github.lsd-consulting:lsd-junit5:3.1.0'
    integrationHttpTestImplementation 'io.github.lsd-consulting:lsd-distributed-generator:1.1.9'
    integrationHttpTestImplementation('io.github.lsd-consulting:lsd-distributed-generator-ui:3.1.2:wiremock-stubs'){
        exclude group: "*", module: "*"
    }

    integrationHttpTestImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.1'
}

task integrationHttpTest(type: Test) {
    useJUnitPlatform()
    dependsOn assemble

    testClassesDirs = sourceSets.integrationHttpTest.output.classesDirs
    classpath = sourceSets.integrationHttpTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = false
    mustRunAfter(test)
    finalizedBy jacocoTestReport
}

check.dependsOn(integrationHttpTest)

integrationHttpTest {
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Http integration test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
