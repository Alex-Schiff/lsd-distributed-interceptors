import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.12.0'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.2'
}

group = 'com.integreety'
project.version = scmVersion.version
println "Build Version = ${project.version}"

sourceCompatibility = '11'
targetCompatibility = '11'

bootJar {
    enabled = false
}

jar {
    enabled = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    systemProperty 'yatspec.output.dir', "$buildDir/reports/yatspec"
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = 'lsd-distributed-interceptor-library'
            version = scmVersion.version

            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'lsd-distributed-interceptor-library'
                description = 'A set of interceptors gathering information for the LSD (Yatspec) library.'
                url = 'https://github.com/nickmcdowall/yatspec.git'
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Integreety Technology"
                        email = "admin@integreety.co.uk"
                        organization = 'Integreety Ltd.'
                        organizationUrl = 'https://github.com/integreety'
                    }
                }
                scm {
                    url = "https://github.com/integreety/lsd-distributed-interceptor-library.git"
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

apply plugin: 'io.spring.dependency-management'
dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.1'
        mavenBom 'io.pivotal.spring.cloud:spring-cloud-services-dependencies:3.2.0.RELEASE'
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Database driver for storing interactions
    implementation 'org.mongodb:mongo-java-driver:3.12.6'

    // Yatspec
    implementation 'com.github.nickmcdowall:yatspec:2021.0.2'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    // JSON & XML pretty formatting
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.dom4j:dom4j:2.1.3'

    // Other
    implementation 'org.apache.httpcomponents:httpcore:4.4.13' // Needed for DB connection security
    implementation 'org.apache.commons:commons-lang3:3.11'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'

    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'
    testImplementation 'org.junit.platform:junit-platform-commons:1.7.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.5.13'
    testImplementation 'org.pitest:pitest-junit5-plugin:0.12'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
    testImplementation 'com.github.fridujo:rabbitmq-mock:1.0.14'
    testImplementation 'org.awaitility:awaitility:4.0.3'

    testImplementation 'com.github.tomakehurst:wiremock:2.26.3'

}

apply from: 'pitest.gradle'
