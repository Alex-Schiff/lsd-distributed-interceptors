dependencies {
    testImplementation project(":core")
    testImplementation project(":feign")

    // Spring
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-amqp'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    // Other
    testImplementation 'com.github.fridujo:rabbitmq-mock:1.1.1'
    testImplementation 'org.awaitility:awaitility:4.1.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'
    testImplementation 'org.jeasy:easy-random-core:5.0.0'

    // LSD
    testImplementation 'io.github.lsd-consulting:lsd-distributed-http-connector:3.0.0'
    testImplementation 'io.github.lsd-consulting:lsd-junit5:5.2.0'
    testImplementation 'io.github.lsd-consulting:lsd-distributed-generator:6.0.0'
    testImplementation('io.github.lsd-consulting:lsd-distributed-generator-ui-api:5.0.0')
    testImplementation('io.github.lsd-consulting:lsd-distributed-generator-ui-service:5.0.0:wiremock-stubs'){
        exclude group: "*", module: "*"
    }

    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.1'
}

test {
    useJUnitPlatform()
    testLogging.events("skipped", "failed")
    systemProperty("lsd.core.report.outputDir", "$buildDir/reports/lsd")
    afterSuite { desc, result ->
        if (!desc.parent) {
            logger.lifecycle("Unit test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

compileTestKotlin.kotlinOptions.jvmTarget = "17"
